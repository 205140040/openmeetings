<?xml version="1.0"?>
<!--
  Licensed to the Apache Software Foundation (ASF) under one
  or more contributor license agreements.  See the NOTICE file
  distributed with this work for additional information
  regarding copyright ownership.  The ASF licenses this file
  to you under the Apache License, Version 2.0 (the
  "License"); you may not use this file except in compliance
  with the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing,
  software distributed under the License is distributed on an
  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
  KIND, either express or implied.  See the License for the
  specific language governing permissions and limitations
  under the License.

-->
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		width="570" height="900" pageTitle="Openmeetings" fontSize="12"
		applicationComplete="appInit(event)" uncaughtError="uncaughtError(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<mx:TraceTarget/>
	</fx:Declarations>
	<fx:Script><![CDATA[
		import mx.core.FlexGlobals;
		import mx.graphics.SolidColor;
		import mx.utils.UIDUtil;
		import flash.events.MouseEvent;

		import org.apache.openmeetings.OmVideo;

		private var audioOnly:Boolean = false;
		private var FPS:int;
		private var bandwidth:int = 0;
		private var quality:int = 100;
		private var echoPath:int = 256;
		private var echoSuppression:Boolean = true;
		private var micRate:int = 22;//5, 8, 11, 22, and 44
		private var selectedMic:int = -1;
		private var selectedCam:int = -1;
		private var video:OmVideo;
		private var recName:String;
		private var mic:Microphone = null;
		private var activities:Array = [];
		private var rights:Array = [];
		private var activityTimer:Timer = new Timer(100);
		private var active:Boolean = false;
		private var userId:String = null;
		[Bindable]
		private var interview:Boolean = false;
		[Embed(source="assets/micro.png")]
		[Bindable]
		public var micImgCls:Class;
		[Bindable]
		private var exclusiveTitle:String;

		private function debug(...rest):void {
			ExternalInterface.call("console.log", rest);
		}

		private function camAvail():Boolean {
			return !audioOnly && Camera.names.length > 0;
		}

		private function micAvail():Boolean {
			return Microphone.names.length > 0;
		}

		private function _hasAudio(act:Array):Boolean {
			return act.indexOf('broadcastA') > -1;
		}

		private function hasAudio():Boolean {
			return _hasAudio(activities);
		}

		private function _hasVideo(act:Array):Boolean {
			return act.indexOf('broadcastV') > -1;
		}

		private function hasVideo():Boolean {
			return _hasVideo(activities);
		}

		private function getIntParam(param:String, def:int):int {
			var val:int = parseInt(param);
			return (isNaN(val) || val < 1 ? def : val);
		}

		private function appInit(evt:Event):void {
			var params:Object = FlexGlobals.topLevelApplication.parameters;
			debug("appInit()", params);
			audioOnly = 'true' == params.audioOnly;
			interview = 'true' == params.interview;
			exclusiveTitle = params.exclusiveTitle;
			FPS = getIntParam("fps", 30);
			bandwidth = getIntParam(params.bandwidth, 0);
			quality = getIntParam(params.quality, 90);
			echoPath = getIntParam(params.echoPath, 128);
			micRate = getIntParam(params.micRate, 22);
			video = new OmVideo(videoDisplay, params);
			activities = params.av == null ? [] : params.av.split(',');
			rights = params.rights == null ? [] : params.rights.split(',');
			userId = params.userId;
			switch (params.mode) {
				case 'settings': {
					ExternalInterface.addCallback("getDevices", function ():Object {
						return {
							cams: Camera.names
							, mics: Microphone.names
						};
					});
					ExternalInterface.addCallback("camChanged", function (val:int):void {
						selectedCam = val;
						camChanged(null);
					});
					ExternalInterface.addCallback("micChanged", function (val:int):void {
						selectedMic = val;
						camChanged(null);
					});
					ExternalInterface.addCallback("resChanged", function (width:int, height:int):void {
						setResolution(width, height, true);
					});
					ExternalInterface.addCallback("close", function ():void {
						video.reset();
					});
					ExternalInterface.addCallback("init", function (camIdx:int, micIdx:int, width:int, height:int):void {
						selectedCam = camIdx;
						selectedMic = micIdx;
						setResolution(width, height, true);
					});
					ExternalInterface.addCallback("startRec", function ():void {
						startTestRecording();
					});
					ExternalInterface.addCallback("play", function ():void {
						playTestRecording();
					});
					ExternalInterface.call("VideoSettings.initSwf");
				}
					break;
				case OmVideo.BROADCAST: {
					selectedCam = params.cam;
					selectedMic = params.mic;
					_resize(Math.max(300, params.width), Math.max(200, params.height));
					ExternalInterface.addCallback("setVolume", function (vol:int):void {
						if (hasAudio()) {
							video.setVolume(vol);
						}
					});
					ExternalInterface.addCallback("rightsUpdated", rightsUpdatedCallback);
					video.connect(function ():void {
						// trust no-one
						video.getNc().call("check", new Responder(function (check:Object):void {
							debug("check", check);
							audioOnly = check.audioOnly;
							activities = check.activities;
							interview = 'interview' == check.roomType;
							attachCamera(function ():void {
								// trust no-one
								video.getNc().call("check", new Responder(function (check:Object):void {
									debug("check", check);
									activities = check.activities;
									addImage(params.width, params.height);
									_resize(params.width, params.height);
									_updateExclusive();
									activityTimer.addEventListener(TimerEvent.TIMER, broadcastTimerListener);
									refreshBroadcast(null);
									ExternalInterface.call("VideoManager.securityMode", params.uid, false);
									ExternalInterface.addCallback("refresh", refreshBroadcast);
									ExternalInterface.addCallback("update", function ():void {
										video.getNc().call("check", new Responder(function (check:Object):void {
											debug("check", check);
											var pVideo:Boolean = hasVideo(), nVideo:Boolean = _hasVideo(check.activities)
													, pAudio:Boolean = hasAudio(), nAudio:Boolean = _hasAudio(check.activities);
											if (nVideo != pVideo || nAudio != pAudio) {
												// activities are changed
												activities = check.activities;
												if (nVideo && pVideo) {
													nAudio ? video.unmute() : video.mute();
													if (mic != null) {
														activityTimer.start();
													} else {
														activityTimer.stop();
													}
												} else {
													refreshBroadcast(null);
												}
											}
											_updateExclusive();
											//debug("Client updated: ", c);
										}));
									});
								}));
							});
						}))
					});
				}
					break;
				case OmVideo.PLAY: {
					addImage(params.width, params.height);
					_resize(params.width, params.height);
					refreshCallback(null);
					ExternalInterface.addCallback("setVolume", function (vol:int):void {
						video.setStreamVolume(vol);
					});
					ExternalInterface.addCallback("update", function (c:Object):void {
						//no-op for now
					});
					ExternalInterface.addCallback("refresh", refreshCallback);
					ExternalInterface.addCallback("rightsUpdated", rightsUpdatedCallback);
				}
					break;
			}
			ExternalInterface.addCallback("vidResize", vidResize);
		}

		private function _updateExclusive():void {
			if (hasAudio()) {
				addEventListener(MouseEvent.MOUSE_OUT, exclMouseOutHandler);
				addEventListener(MouseEvent.MOUSE_OVER, exclMouseOverHandler);
				addEventListener(MouseEvent.CLICK, exclMouseClickHandler);
			} else {
				removeEventListener(MouseEvent.MOUSE_OUT, exclMouseOutHandler);
				removeEventListener(MouseEvent.MOUSE_OVER, exclMouseOverHandler);
				removeEventListener(MouseEvent.CLICK, exclMouseClickHandler);
			}
		}

		private function exclMouseOutHandler(event:MouseEvent):void {
			exclusiveMic.visible = false;
		}

		private function exclMouseOverHandler(event:MouseEvent):void {
			exclusiveMic.visible = true;
		}

		private function exclMouseClickHandler(event:MouseEvent):void {
			//exclusiveMic.visible = true;
		}

		private function _resize(_width:int, _height:int):void {
			userImage.width = _width;
			userImage.height = _height;
			exclusiveMic.x = (_width - exclusiveMic.width) / 2;
			exclusiveMic.y = (_height - exclusiveMic.height) / 2;
			video.resize(_width, _height);
		}

		private function vidResize(_width:int, _height:int):void {
			_resize(_width, _height);
			video.vidResize(_width, _height);
		}

		private function refreshBroadcast(opts:Object):void {
			if (opts && !isNaN(opts.width)) {
				selectedCam = opts.cam;
				selectedMic = opts.mic;
				vidResize(opts.width, opts.height);
			}
			video.reset();
			activityTimer.stop();
			var broadcastId:String = UIDUtil.createUID();
			debug("BroadcastId = " + broadcastId);
			mic = getMic();
			if (mic != null) {
				volumeRect.visible = true;
				volumeRect.fill = new SolidColor(0x00FF00);
				activityTimer.start();
			}
			video.broadcast(broadcastId, hasVideo() ? getCam() : null, mic);
			if (!hasAudio()) {
				video.mute();
			} else {
				video.resetVolume();
			}
		}

		private function rightsUpdatedCallback(rights:Array):void {
			debug("rightsUpdatedCallback", rights);
		}

		private function refreshCallback(opts:Object):void {
			var params:Object = FlexGlobals.topLevelApplication.parameters;
			video.reset();
			video.play(params.broadcastId); // TODO audio/video
			video.resetStreamVolume();
		}

		private function addImage(_width:int, _height:int):void {
			userImage.source = 'profile/' + userId + '?anti=' + new Date().time;
			userImage.visible = true;
		}

		private function uncaughtError(e:UncaughtErrorEvent):void {
			debug("Unexpected ERROR", e);
		}

		private function getMic():Microphone {
			debug("Entering getMic ...");
			var _micro:Microphone = null;
			if (selectedMic > -1) {
				_micro = echoPath == 0 ? Microphone.getMicrophone(selectedMic) : Microphone.getEnhancedMicrophone(selectedMic);

				if (_micro != null) {
					if (echoPath == 256) {
						var options:MicrophoneEnhancedOptions = new MicrophoneEnhancedOptions();
						options.mode = MicrophoneEnhancedMode.FULL_DUPLEX;
						options.echoPath = echoPath;
						options.nonLinearProcessing = true;
						_micro.enhancedOptions = options;
						debug("echoPath set to " + echoPath + ", " + _micro.enhancedOptions);
					}
					_micro.codec = SoundCodec.NELLYMOSER;
					_micro.framesPerPacket = 1;
					_micro.setSilenceLevel(0, 2000);
					debug("canvas.micRate: " + micRate);
					_micro.rate = micRate;
					_micro.gain = 50;
					// this has no effect if enhanced microphone is obtained
					//Microphone setUseEchoSupression(bool)
					debug("canvas.echoSuppression: " + echoSuppression);
					_micro.setUseEchoSuppression(echoSuppression);
				}
				debug("... getMic DONE" + _micro);
			}
			return _micro;
		}

		private function getCam():Camera {
			debug("Entering getCam ...");
			var _camera:Camera = null;
			if (selectedCam > -1) {
				_camera = Camera.getCamera("" + selectedCam);
				if (_camera != null && !_camera.muted) {
					//FIXME need to be unified
					if (interview) {
						//we need a fixed frame rate for the videos to merge them later on
						_camera.setMode(video.width, video.height, 24);
						debug("IS INTERVIEW ");
						_camera.setQuality(0, 98);
					} else {
						_camera.setMode(video.width, video.height, FPS);
						debug("IS NO INTERVIEW ");
						_camera.setQuality(bandwidth, quality);
					}
				}
				debug("... getCam DONE " + _camera);
			}
			return _camera;
		}

		private function attachCamera(callback:Function):void {
			if (!camAvail()) {
				return;
			}
			var params:Object = FlexGlobals.topLevelApplication.parameters;
			debug("Camera selected:: " + selectedCam);
			var cam:Camera = getCam();
			debug("Camera selected:: " + cam, cam == null ? '' : cam.muted);
			if (cam != null) {
				if (cam.muted) {
					debug("Camera Muted");
					if (params.mode != 'settings') {
						ExternalInterface.call("VideoManager.securityMode", params.uid, true);
					}
					video.attachCamera(cam);
					cam.addEventListener(StatusEvent.STATUS, function (event:StatusEvent):void {
						debug("cameraStatusHandler! " + event);
						cam.removeEventListener(StatusEvent.STATUS, arguments.callee);
						switch (event.code) {
							case 'Camera.Muted':
								debug("Unable to connect to active camera.");
								break;
							case 'Camera.Unmuted':
								ExternalInterface.call("VideoSettings.allowRec", true);
								callback();
								break;
						}
					});
				} else {
					callback();
				}
			} else {
				var _mic:Microphone = getMic();
				if (_mic != null) {
					if (_mic.muted) {
						debug("Mic Muted");
						ExternalInterface.call("VideoManager.securityMode", params.uid, true);
						var nc:NetConnection = new NetConnection();
						nc.connect(null);
						var ns:NetStream = new NetStream(nc);
						ns.attachAudio(_mic);
						_mic.addEventListener(StatusEvent.STATUS, function (event:StatusEvent):void {
							debug("micStatusHandler! " + event);
							_mic.removeEventListener(StatusEvent.STATUS, arguments.callee);
							ns.close();
							nc.close();
							if (_mic.muted) {
								debug("Unable to connect to active microphone.");
							} else {
								ExternalInterface.call("VideoSettings.allowRec", true);
								callback();
							}
						});
					} else {
						callback();
					}
				}
			}
		}

		private function _attachCamera(cam:Camera):void {
			debug("_attachCamera():: muted ? " + (cam == null ? '' : cam.muted));
			if (cam != null && cam.muted) {
				debug("Unable to connect to active camera.");
			} else {
				try {
					video.reset();
					video.attachCamera(cam);
					debug("_attachCamera()::done");
				} catch (error:Error) {
					debug("_attach:: " + error.message + "\n" + error.getStackTrace());
				}
			}
		}

		private function settingsCameraCallback():void {
			ExternalInterface.call("VideoSettings.allowRec", true);
			_attachCamera(getCam());
		}

		private function setResolution(width:int, height:int, attach:Boolean):void {
			if (!interview) {
				debug("onselect WxH :: " + width + "x" + height);

				video.resize(width, height);

				if (attach) {
					attachCamera(settingsCameraCallback);
				}
			}
		}

		private function camChanged(e:Event):void {
			debug("camChanged");
			attachCamera(settingsCameraCallback);
		}

		private function playTestRecording():void {
			video.play(recName + ".flv");
		}

		private function testTimerListener(event:TimerEvent):void {
			ExternalInterface.call("VideoSettings.micActivity", mic.activityLevel);
		}

		private function broadcastTimerListener(event:TimerEvent):void {
			var params:Object = FlexGlobals.topLevelApplication.parameters;
			var _active:Boolean = mic.activityLevel > 8;
			var h:int = video.height - 10;
			var ah:int = Math.round(mic.activityLevel * h / 100);
			volumeRect.y = 5 + h - ah;
			volumeRect.height = ah;
			if (active !== _active) {
				active = _active;
				video.getNc().call("micActivity", new Responder(function (res:Object):void {
				}), active);
				ExternalInterface.call("VideoManager.micActivity", params.uid, active);
			}
		}

		private function startTestRecording():void {
			try {
				var counter:int = 5;
				timerText.visible = true;
				timerText.text = "5 sec";
				var recTimer:Timer = new Timer(1000, counter);
				var t:Date = new Date();
				recName = "TEST_SETUP_" + t.getTime();
				mic = getMic();
				activityTimer.addEventListener(TimerEvent.TIMER, testTimerListener);
				video.record(recName, getCam(), mic, function ():void {
					if (mic != null) {
						activityTimer.start();
					}
					recTimer.addEventListener(TimerEvent.TIMER, function (event:TimerEvent):void {
						timerText.text = --counter + " sec";
						if (counter == 0) {
							timerText.visible = false;
							ExternalInterface.call("VideoSettings.allowPlay");
							playTestRecording();
							activityTimer.stop();
							activityTimer.removeEventListener(TimerEvent.TIMER, testTimerListener);
							mic = null;
						}
					});
					recTimer.start();
				});
			} catch (err:Error) {
				debug("ERROR: " + err);
			}
		}
		]]></fx:Script>

	<s:Image id="userImage" visible="false" width="0" height="0" />
	<mx:UIComponent id="videoDisplay" width="0" height="0" />
	<s:Rect id="volumeRect" x="5" y="5" width="10" height="100" visible="false" />
	<s:Image id="exclusiveMic" source="{micImgCls}" toolTip="{exclusiveTitle}" visible="false"/>
	<s:Label id="timerText" height="20" width="45" x="20" y="5" paddingLeft="5" paddingTop="5"
			 visible="false" backgroundColor="0xf5f5f5" fontWeight="bold"><s:text></s:text></s:Label>
</s:Application>
